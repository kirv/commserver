#!/usr/bin/expect -f

# request-commserver-session SITE HOST PORT
# connect to commserver (via inetd.conf on remote host)
# enter 'call site' command at 'query:' prompt
# wait for either PASSED or FAILED string and:
#   if FAILED, exit with status 1
#   if PASSED, exit with status 0

set site ""
set host ""
set port ""
set tmout 300
set opts ""
set routefile ""

foreach arg $argv {
    regexp {([^=]+)=(.*)} $arg * tag val
    switch -- $tag {
        "--site" { set site $val }
        "--host" { set host $val }
        "--port" { set port $val }
        "--timeout" { set tmout $val }
        "--store-route" { set routefile $val }
        default { lappend opts $arg }
        }
    }

if { $site == "" || $host == "" || $port == "" } {
    puts "synopsis: $argv0 --site=SITE --host=HOST --port=PORT \[--timeout=SEC\] \[--store-route=FILE\] \[...\]"
    exit 1
    }

spawn nc $host $port

set timeout 2
expect {
    timeout { puts "no prompt from server"; exit 1 }
    query: { send "call $site --notify --timeout=$tmout $opts\r" }
    }

set timeout [expr $tmout+10]
expect {
    FAILED { exit 1 }
    "PASSED *" {
        if { $routefile == "" } { exit 0 }
        if [catch {set file [open $routefile w]} errormsg] {
            puts "warning: $errormsg"
            exit 0
            }
        # file handle is available for writing
        # string index 7 is the first character of the radio path
        puts $file [string range $expect_out(buffer) 7 end]
        close $file
        exit 0
        }
    PASSED { exit 0 }
    }

puts "no result from server"
exit 2
